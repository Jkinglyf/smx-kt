group 'com.wxmlabs'
version '0.1'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

buildscript {
    ext{
        kotlin_version = '1.2.10'
        dokka_version = '0.9.15'
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

apply plugin: 'kotlin-platform-jvm'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6
archivesBaseName = 'smx'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    expectedBy project(":smx-core")
    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

dokka {
    jdkVersion = 6
    reportNotDocumented = true
    skipEmptyPackages = true
    impliedPlatforms = ["JVM"]
    sourceRoot {
        path = "src"
        platforms = ["JVM"]
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokkaJavadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUserName, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUserName, password: ossrhPassword)
            }

            pom.project {
                name 'SMx Algorithms'
                packaging 'jar'
                description 'SM2/SM3/SM4 Algorithms'
                url 'https://github.com/wxmlabs/smx-kt'

                scm {
                    connection 'scm:git:git://github.com/wxmlabs/smx-kt.git'
                    developerConnection 'scm:git:ssh://github.com:wxmlabs/smx-kt.git'
                    url 'https://github.com/wxmlabs/smx-kt/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'shiningwang'
                        name 'Wang Xuanmin'
                        email 'shiningwang@vshining.com'
                    }
                }
            }
        }
    }
}
